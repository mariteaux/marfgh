; "Long cheats" is what the actual game cheats (as opposed to debug functions
; with the DualShock or keyboard) are called. To confuse people less, we
; renamed the debug scripts and left this one as-is.
; 
; Most of the vanilla cheats were moved to the Extras menu. This serves two
; important functions though, defining the combinations for the Modifiers
; menu (yes, long cheats work in places other than the main menu!) and easter
; egg screens.

; Hoo! This is a nice, big eyeful of DTA conditional structure. To explain,
; && returns true if all conditions match. What this is saying is that if
; the UI object exists (meaning you're in the menus and not in a song), that
; UI is not currently in between two screens, and we're on the main menu, the
; cheat can be performed.
; We only use this for the vanilla cheats. Long cheats can be performed in
; most menus, so we leave this off so people can access the Modifiers menu
; or access the eggs on other menus.
#define CHEATS_ALLOWED
({&&
   {exists ui}
   {! {ui in_transition}}
   {== {ui current_screen} main_screen}})

#define G_GREEN (kPad_R2)
#define G_RED (kPad_Circle)
#define G_YELLOW (kPad_Tri)
#define G_BLUE (kPad_X)
#define G_ORANGE (kPad_Square)

; If we're on a setlist and we're not transitioning to another screen, move
; to the Modifiers menu, saving the previous screen so we can go back to it
; afterwards.
((kPad_Select kPad_Select)
   {if {&& {exists ui} {! {ui in_transition}}
      {'||'
         {== {ui current_screen} qp_selsong_screen}
         {== {ui current_screen} multi_vs_selsong_screen}
         {== {ui current_screen} multi_coop_selsong_screen}
         {== {ui current_screen} multi_fo_selsong_screen}}}
      {set $modifier_exit {ui current_screen}}
      {synth play_sequence button_select}
      {ui goto_screen modifiers_screen}})

; Uncensored songs
((G_RED G_ORANGE G_BLUE G_BLUE G_ORANGE G_ORANGE G_BLUE G_YELLOW)
   {if CHEATS_ALLOWED
      {if_else $cheat_enabled_uncensored
         {do
            {set $cheat_enabled_uncensored FALSE}
            {main_screen display_cheat_msg cheat_uncensored FALSE}
            {play_sfx cheat_off}}
         {do
            {set $cheat_enabled_uncensored TRUE}
            {main_screen display_cheat_msg cheat_uncensored TRUE}
            {play_sfx cheat}}}})