{new UIPanel options_panel
   (file options.milo)
   (focus op_audio.btn)
   (enter
      {game set_song_index 0} ; fixes the dumb random "song not found" crash when you go into credits twice because the song is already loaded
	  {op_title.lbl set_local_pos 0 -100 20}
      {op_buttons.view set_local_pos -50 0 -20}
      {op_bonus.btn set_showing FALSE}
      {op_credit.btn set_showing FALSE}
      {op_bonus.btn set_state kDisabled}
      {op_credit.btn set_state kDisabled})
   (SELECT_START_MSG
      {switch
         $component
         (op_audio.btn
            {ui goto_screen audio_settings_screen})
         (video_settings.btn
            {ui goto_screen video_settings_screen})
         (op_data.btn
            {play_bonus_video_screen set video {{game video_provider} get_video_file video2}}
            {ui goto_screen play_bonus_video_screen})
         (memory_card.btn
            {gamecfg set mode credits}
            {game_screen set attract_mode FALSE}
            {{game get_player_config 0} set_difficulty kDifficultyExpert}
            {game set_character goth2 TRUE}
            {game set_quickplay}
            {game set_song countdowntoinsanity}
            {meta_loading_nosave_goto credits_game_screen})
         (op_bonus.btn)
         (op_credit.btn)}
      kDataUnhandled)}
{new GHScreen options_screen
   (panels meta options_panel helpbar)
   (focus options_panel)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))}
#define SLIDER_LEVELS (12)
#define AUDIO_SETTINGS_PANEL_HANDLERS
((focus gs_band.sld)
   (focus_component "")
   (reset_focus TRUE)
   (orig_vol 0)
   (finish_load
      {gs_band.sld set_num_steps SLIDER_LEVELS}
      {gs_guitar.sld set_num_steps SLIDER_LEVELS}
      {gs_sfx.sld set_num_steps SLIDER_LEVELS})
   (slider_changed
      {'||'
         {!= {options get band_volume_idx}
            {gs_band.sld current}}
         {!= {options get guitar_volume_idx}
            {gs_guitar.sld current}}
         {!= {options get fx_volume_idx}
            {gs_sfx.sld current}}})
   (enter
      {if {== {ui current_screen} audio_settings_screen}
         {gs_title.lbl set_local_pos 0 0 185}}
      {gs_band.sld set_current
         {options get band_volume_idx}}
      {gs_guitar.sld set_current
         {options get guitar_volume_idx}}
      {gs_sfx.sld set_current
         {options get fx_volume_idx}}
      {stereo.chk set_check
         {options get_stereo}})
   (set_volumes
      {if {$this slider_changed}
         {options set band_volume_idx {gs_band.sld current}}
         {options set guitar_volume_idx {gs_guitar.sld current}}
         {options set fx_volume_idx {gs_sfx.sld current}}})
   (slider_select_cancel
      ($component)
      {switch $component
         (gs_band.sld
            {$component set_current [orig_vol]})
         (gs_guitar.sld
            {$component set_current [orig_vol]})
         (gs_sfx.sld
            {$component set_current [orig_vol]})}
      {$this update_help_display}
      kDataUnhandled)
   (FOCUS_MSG
      {$this set focus_component {$new_focus name}}
      {$this update_help_display})
   (update_help_display
      {if_else {$this slider_selected}
         {helpbar set_display
            ((fret1 help_confirm)
               (fret2 help_cancel)
               (strum help_adjvol))}
         {do
            {if_else {== [focus_component] "gs_stereo.btn"}
               {helpbar set_display
                  ((fret1 help_onoff)
                     (fret2 help_back)
                     (strum help_updown))}
               {helpbar set_display
                  ((fret1 help_select)
                     (fret2 help_back)
                     (strum help_updown))}}}})
   (slider_start_msg
      ($component)
      {do
         ($vol {options get_volume_from_idx {$component current}})
         {switch
            $component
            (gs_band.sld
               {play_sfx slider_band (volume $vol) (stop TRUE)})
            (gs_guitar.sld
               {play_sfx slider_gtr (volume $vol) (stop TRUE)})
            (gs_sfx.sld
               {play_sfx slider_sfx (volume $vol) (stop TRUE)})}})
   (SELECT_START_MSG
      {switch
         $component
         (gs_band.sld
            {$this set orig_vol {gs_band.sld current}})
         (gs_guitar.sld
            {$this set orig_vol {gs_guitar.sld current}})
         (gs_sfx.sld
            {$this set orig_vol {gs_sfx.sld current}})
         (gs_stereo.btn
            {stereo.chk toggle}
            {options set_stereo {stereo.chk get_check}})
         kDataUnhandled}
      {$this update_help_display}))
{new SliderPanel audio_settings_panel
   (file game_settings.milo)
   AUDIO_SETTINGS_PANEL_HANDLERS}
{new GHScreen audio_settings_screen
   (panels meta audio_settings_panel helpbar)
   (focus audio_settings_panel)
   (allow_back FALSE)
   (BUTTON_DOWN_MSG
      {if {&& {! {audio_settings_panel slider_selected}} {== $button kPad_Tri}}
         {$this backwards_anim}
         {synth play_sequence button_back.cue}
         {$this set_volumes}
         {autosave_goto options_screen}}
      kDataUnhandled)}
{new SliderPanel pause_audio_settings_panel
   (file pause_audio_settings.milo)
   AUDIO_SETTINGS_PANEL_HANDLERS}
{new GHScreen pause_audio_settings_screen
   (panels GAME_PANELS pause_audio_settings_panel helpbar)
   (focus pause_audio_settings_panel)
   (in_game TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (BUTTON_DOWN_MSG
      {if {&& {! {pause_audio_settings_panel slider_selected}} {== $button kPad_Tri}}
         {$this set_volumes}
         {ui goto_screen pause_screen}}
      kDataUnhandled)}
{new GHScreen play_bonus_video_screen
   (allow_back FALSE)
   (animate_transition FALSE)
   (video "")
   (TRANSITION_COMPLETE_MSG
      {meta set_tool_movie TRUE}
      {meta play_movie [video]}
      {meta set_tool_movie FALSE}
      {ui goto_screen options_screen})}
; love you milohax for the lag stuffs <3
; courtesy of https://github.com/hmxmilohax/gh2-calibration-fix
; i use the digital out on my ps2 so audio lag doesn't really affect me but i know most people do have to deal with it
{new LagPanel lag_panel
   (file lag.milo)
   (focus autocalibrate.btn)
   (from_panel "")
   (state init)
   (hits ())
   (seconds ())
   (window 50)
   (min_hits 11) ; vanilla: 7
   (min_avg -500) ; vanilla: -100
   (max_avg 500) ; vanilla: 250
   (update_groups
      {cond
         ({== [state] calibrating}
            {buttons.grp set showing FALSE})
         (TRUE
            {buttons.grp set showing TRUE})})
   (update_helpbar
      {do
         ($array)
         {set $array ()}
         {resize $array 0}
         {cond
            ({== [state] calibrating}
               {push_back $array (strum help_hitonchange)})
            ({'||' {== [state] adjusting_audio} {== [state] adjusting_video}}
               {push_back $array (fret1 help_accept)}
               {push_back $array (fret2 help_cancel)}
               {push_back $array (fret3 help_reset)} ; no clue why you had trouble replacing fret3, i got it working just fine for ps2 at least
               {push_back $array (strum help_adjsync)})
            (TRUE
               {push_back $array (fret1 help_select)}
               {push_back $array (fret2 help_back)}
               {push_back $array (strum help_updown)})}{helpbar set_display $array}})
   (update_text
      {switch [state]
         (init
            {instructions.lbl set text_token lag_info_why}
            {instructions2.lbl set text_token ""}
            {setting.lbl set_showing FALSE}
            {autocalibrate.btn set text_token lag_button_calibrate}
            {$this enable autocalibrate.btn}
            {reset_to_zero.btn set text_token {sprintf {localize lag_audio_setting} {int $lag_audio}}}
            {$this enable reset_to_zero.btn}
            {btn3.btn set text_token {sprintf {localize lag_video_setting} {int $lag_video}}}
            {$this enable btn3.btn})
         (calibrating
            {instructions.lbl set text_token lag_measuring}
            {instructions2.lbl set text_token ""})
         (success
            {set $lag_video $lag_video_dirty}  ; apply video cal
            {instructions.lbl set text_token lag_success}
            {instructions2.lbl set_localized ""}
            {autocalibrate.btn set text_token lag_button_recalibrate}
            {reset_to_zero.btn set text_token {sprintf {localize lag_audio_setting} {int $lag_audio}}}
            {btn3.btn set text_token {sprintf {localize lag_video_setting} {int $lag_video}}})
         (failure
            {instructions.lbl set text_token lag_info_failure}
            {instructions2.lbl set text_token ""}
            {autocalibrate.btn set text_token lag_button_recalibrate}
            {reset_to_zero.btn set text_token {sprintf {localize lag_audio_setting} {int $lag_audio}}}
            {btn3.btn set text_token {sprintf {localize lag_video_setting} {int $lag_video}}})
         (adjusting_audio
            {$this disable autocalibrate.btn}
            {$this disable btn3.btn}
            {reset_to_zero.btn set text_token {sprintf {localize lag_audio_setting} {int $lag_audio}}})
         (adjusting_video
            {$this disable autocalibrate.btn}
            {$this disable reset_to_zero.btn}
            {btn3.btn set text_token {sprintf {localize lag_video_setting} {int $lag_video}}})})
   (set_state ($s)
      {set [state] $s}
      {$this update_groups}
      {$this update_helpbar}
      {$this update_text})
   (enter
      ; new objects
      {new BandButton btn3.btn}
      {btn3.btn copy reset_to_zero.btn kCopyShallow}
      {reset_to_zero.btn set nav_down btn3.btn}
      {btn3.btn set_local_pos -237 0 -130}
      {$this sync_objects}
      {countdown.lbl set_localized ""}
      {$this set_state init}
      {set [hits] {array 0}}
      {set [seconds] {array 0}})
   (exit {set_calibration})  ; apply settings, but no milo writing because no saving on marfgh
   (BUTTON_DOWN_MSG
      {if {== [state] adjusting_audio}
         {if {== $button kPad_DDown}
            {synth play_sequence button_toggle}
            {set $lag_audio {- $lag_audio 1.0}}
            {if {< $lag_audio [min_avg]}
               {set $lag_audio [min_avg]}}}
         {if {== $button kPad_DUp}
            {synth play_sequence button_toggle}
            {set $lag_audio {+ $lag_audio 1.0}}
            {if {> $lag_audio [max_avg]}
               {set $lag_audio [max_avg]}}}
         {if {== $button kPad_Circle}
            {play_sfx button_error}
            {set $lag_audio 0}}
         {$this update_text}}
      {if {== [state] adjusting_video}
         {if {== $button kPad_DDown}
            {synth play_sequence button_toggle}
            {set $lag_video {- $lag_video 1.0}}
            {if {< $lag_video [min_avg]}
               {set $lag_video [min_avg]}}}
         {if {== $button kPad_DUp}
            {synth play_sequence button_toggle}
            {set $lag_video {+ $lag_video 1.0}}
            {if {> $lag_video [max_avg]}
               {set $lag_video [max_avg]}}}
         {if {== $button kPad_Circle}
            {play_sfx button_error}
            {set $lag_video 0}}
         {$this update_text}}
      {if_else {== [state] calibrating}
         {if {'||' {== $button kPad_DDown} {== $button kPad_DUp}}
            {push_back [hits] {taskmgr ui_seconds}}}
         {switch $button
            (kPad_Tri ; to support cancelling, pressing red reverts to last
               {if_else {'||' {== [state] adjusting_audio} {== [state] adjusting_video}}
                  {do
                     {if {== [state] adjusting_audio}
                        {set $lag_audio $lag_audio_last}}
                     {if {== [state] adjusting_video}
                        {set $lag_video $lag_video_last}}
                     {$this set_state init}
                     {synth play_sequence button_back.cue}}
                  {do ; set focus to the top button on exit so it doesnt crash on re-enter
                     {$this set_focus autocalibrate.btn}
                     {reset_to_zero.btn set nav_down autocalibrate.btn}
                     {if_else {== [from_panel] video_settings_panel}
                        {ui goto_screen video_settings_screen}
                        {ui goto_screen pause_video_settings_screen}}}})
            kDataUnhandled}})
   (SELECT_START_MSG
      {switch $component
         (autocalibrate.btn
            {$this set_state calibrating}
            {$this start_audio_countdown})
         (reset_to_zero.btn
            {if_else {== [state] adjusting_audio}
               {$this set_state init}
               {do
                  {set $lag_audio_last $lag_audio}
                  {$this set_state adjusting_audio}}})
         (btn3.btn
            {if_else {== [state] adjusting_video}
               {$this set_state init}
               {do
                  {set $lag_video_last $lag_video}
                  {$this set_state adjusting_video}}})})
   (play_sync_animation {lag animate})
   (start_audio_countdown ; audio test
      {set $lag_aniamte_thread_wait TRUE}
      {set $lag_animate_thread_loop TRUE}
      {thread_task
         (units kTaskUISeconds)
         (script ; yes hmx actually did it like this
            {instructions.lbl set text_token lag_audio_test}
            {instructions2.lbl set text_token lag_info_howto_audio_test}
            {$task sleep 2}
            {countdown.lbl set text_token lag_3}
            {practice_hat play}
            {$task sleep 0.133}
            {$task sleep 0.6}
            {practice_hat play}
            {countdown.lbl set text_token lag_2}
            {$task sleep 0.133}
            {$task sleep 0.6}
            {practice_hat play}
            {countdown.lbl set text_token lag_1}
            {$task sleep 0.133}
            {$task sleep 0.6}
            {practice_hat play}
            {countdown.lbl set text_token lag_go}
            {$task sleep 0.133}
            {resize [hits] 0}
            {resize [seconds] 0}
            {set $lag_audio_dirty 0}  ; clear old result
            ; up num of hits to 16 (previously 11)
            ; {$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            ;{$this play_sync_animation}
            {$task sleep 0.6}
            {sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {countdown.lbl set_localized ""}
            {$task sleep 0.6}
            ; apply cal to temp var
            ; ps2 audio skew results: 27.15, 28.18, 22.98, 26.10, 26.10, 27.15, 29.24, 28.19, 29.19, 29.24, 25.06, 24.01, 25.06, 24.02, 25.06, 31.33, 29.22, 28.18, 28.19, 25.06
            ; 360 audio skew results: 41.61, 41.60, 42.65, 48.89, 49.93, 44.73, 46.83, 43.68, 46.80, 48.88, 46.83, 44.71, 44.74, 44.74, 45.77, 43.69, 45.78
            ; excluded numbers are outliers
            {set $lag_audio_dirty
               {* {int {- {$this calculate_lag [window] [min_hits] [hits] [seconds]} 42.9355}} 1.0}} ; ensure a rounded float
            ; proceed if successful
            {if_else {&& {> $lag_audio_dirty [min_avg]} {< $lag_audio_dirty [max_avg]}}
               {$this start_video_countdown}
               {do
                  {set $lag_audio_dirty 0}
                  {play_sfx button_error}
                  {$this set_state failure}}})})
   ; video test
   (start_video_countdown
      {set $lag_audio $lag_audio_dirty}  ; apply audio cal
      {set $lag_aniamte_thread_wait TRUE}
      {set $lag_animate_thread_loop TRUE}
      {thread_task
         (units kTaskUISeconds)
         (script ; yes hmx actually did it like this
            {synth play_sequence button_toggle}
            {instructions.lbl set text_token lag_video_test}
            {instructions2.lbl set text_token lag_info_howto_video_test}
            {$task sleep 2}
            {countdown.lbl set text_token lag_3}
            {practice_hat play}
            {$task sleep 0.133}
            {$task sleep 0.6}
            {practice_hat play}
            {countdown.lbl set text_token lag_2}
            {$task sleep 0.133}
            {$task sleep 0.6}
            {practice_hat play}
            {countdown.lbl set text_token lag_1}
            {$task sleep 0.133}
            {$task sleep 0.6}
            {practice_hat play}
            {countdown.lbl set text_token lag_go}
            {$task sleep 0.133}
            {resize [hits] 0}
            {resize [seconds] 0}
            {set $lag_video_dirty 0}  ; clear old result
            ; up num of hits to 16 (previously 11)
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {$this play_sync_animation}
            {$task sleep 0.6}
            ;{sync_click.cue play}
            {push_back [seconds] {taskmgr ui_seconds}}
            {$task sleep 0.133}
            {countdown.lbl set_localized ""}
            {$task sleep 0.6}
            ; apply cal to temp var
            ; ps2 video skew results: 21.91, 20.87, 23.99, 21.91, 22.96, 26.07, 21.91, 23.99, 26.09, 22.94, 18.77, 28.16, 26.09, 20.86, 22.96, 23.99, 25.04, 22.97
            ; 360 video skew results: 6.24, -7.30, 3.12, 2.08, -5.22, -0.02, 3.12, -3.13, -0.01, 1.03, -7.32, -3.13, -1.03, -6.26, -3.12, -3.12, 1.04, -5.20, -5.21, -7.32
            ; excluded numbers are outliers
            {set $lag_video_dirty
               {* {int {- {$this calculate_lag [window] [min_hits] [hits] [seconds]} 12.415555555556}} 1.0}} ; ensure a rounded float
            ; proceed if successful
            {if_else {&& {> $lag_video_dirty [min_avg]} {< $lag_video_dirty [max_avg]}}
               {do
                  {synth play_sequence button_toggle}
                  {$this set_state success} }
               {do
                  {set $lag_video_dirty 0}
                  {play_sfx button_error}
                  {$this set_state failure}}})})}
{new GHScreen lag_screen
   (panels lag_panel helpbar)
   (focus lag_panel)}
{new GHScreen pause_lag_screen
   (panels GAME_PANELS lag_panel helpbar)
   (focus lag_panel)
   (clear_vram TRUE)
   (enter
      {set $cal_from_pause TRUE}
      {hud set_showing FALSE}
      {track_panel set_showing FALSE}
      {world_panel set_showing FALSE})}
#define VIDEO_SETTINGS_PANEL_HANDLERS
((focus gs_left_p1.btn)
   (focus_component "")
   (orig_sync 0)
   (enter
      {if {== {ui current_screen} video_settings_screen}
         {vs_title.lbl set_showing FALSE}
         {vs_buttons.view set_local_pos 35 60 -105}
         {game80s.btn set_local_pos -35 0 60}
         {calibrate_lag.btn set_local_pos -35 0 20}}
      {lefty1.chk set_check {options get_lefty 0}}
      {lefty2.chk set_check {options get_lefty 1}}
      {widescreen.chk set_check {options get_widescreen}}
      {p_scan.chk set_check {options get_pscan}}
      {if {== {ui current_screen} video_settings_screen}
         {if_else $camera_control_master
            {game80s.btn set_localized_text {localize authored_cuts_enabled}}
            {game80s.btn set_localized_text {localize authored_cuts_disabled}}}}
      {$this update_help_display}
      {if {&& {== {$this name} pause_video_settings_panel} {! {gamecfg get mode jukebox}}}
         {hud set_showing TRUE}
         {track_panel set_showing TRUE}
         {world_panel set_showing TRUE}})
   (update_help_display
      {if_else {== [focus_component] "calibrate_lag.btn"}
         {helpbar set_display
            ((fret1 help_select)
               (fret2 help_back)
               (strum help_updown))}
         {helpbar set_display
            ((fret1 help_onoff)
               (fret2 help_back)
               (strum help_updown))}})
   (FOCUS_MSG
      {$this set focus_component {$new_focus name}}
      {$this update_help_display})
   (slider_start_msg
      ($component)
      {synth play_sequence button_toggle})
   (slider_select_cancel
      ($component)
      {$this update_help_display}
      kDataUnhandled)
   (SELECT_START_MSG
      {switch $component
         (p_scan.btn
            {if_else {p_scan.chk get_check}
               {do
                  {p_scan.chk toggle}
                  {options set_pscan {p_scan.chk get_check}}}
               {do
                  {if {== $this pause_video_settings_panel}
                     {pause_video_settings_screen set stash_lefty_settings FALSE}}
                  {ui goto_screen [pscan_warning]}}})
         (widescreen.btn
            {widescreen.chk toggle}
            {options set_widescreen {widescreen.chk get_check}})
         (gs_left_p1.btn
            {lefty1.chk toggle}
            {options set_lefty 0 {lefty1.chk get_check}})
         (gs_left_p2.btn
            {lefty2.chk toggle}
            {options set_lefty 1 {lefty2.chk get_check}})
         (game80s.btn
            {authored_cuts_toggle}
            {if_else $camera_control_master
               {game80s.btn set_localized_text {localize authored_cuts_enabled}}
               {game80s.btn set_localized_text {localize authored_cuts_disabled}}})
         (calibrate_lag.btn
            {lag_panel set from_panel $this}
            {if_else {== $this video_settings_panel}
               {ui goto_screen lag_screen}
               {ui goto_screen calibration_restart_screen}})
         kDataUnhandled}
      {$this update_help_display}))
{new SliderPanel video_settings_panel
   (file video_settings.milo)
   VIDEO_SETTINGS_PANEL_HANDLERS
   (pscan_warning pscan_warning_screen)}
{new GHScreen video_settings_screen
   (panels meta video_settings_panel helpbar)
   (focus video_settings_panel)
   (allow_back FALSE)
   (enter
      {$this enable {video_settings_panel find gs_left_p2.btn}})
   (BUTTON_DOWN_MSG
      {if {== $button kPad_Tri}
         {$this backwards_anim}
         {synth play_sequence button_back.cue}
         {autosave_goto options_screen}}
      kDataUnhandled)}
{new GHPanel pscan_warning_panel
   (file pscan_warning.milo)
   (focus pscan_ok.btn)
   (enter
      {dl_text.grp set_local_pos 0 0 10})}
#define PSCAN_WARNING_SCREEN_HANDLERS
((allow_back FALSE)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))
   (SELECT_START_MSG
      {switch
         $component
         (pscan_ok.btn
            {pscan_switch_screen set done_screen [done_screen]}
            {ui goto_screen [switch_screen]})
         (pscan_cancel.btn
            {ui goto_screen [done_screen]})}))
{new GHScreen pscan_warning_screen
   (panels meta pscan_warning_panel helpbar)
   (focus pscan_warning_panel)
   PSCAN_WARNING_SCREEN_HANDLERS
   (done_screen video_settings_screen)
   (switch_screen pscan_switch_screen)}
{new GHPanel pscan_warning_game_panel
   (file pscan_warning_game.milo)
   (focus pscan_ok.btn)}
{new GHScreen pause_pscan_warning_screen
   (panels GAME_PANELS pscan_warning_game_panel helpbar)
   (focus pscan_warning_game_panel)
   PSCAN_WARNING_SCREEN_HANDLERS
   (animate_transition FALSE)
   (done_screen pause_video_settings_screen)
   (switch_screen pause_pscan_switch_screen)}
{new GHPanel pscan_switching_panel
   (file pscan_switching.milo)
   (focus pscan_yes.btn)
   (enter
      {dl_text.grp set_local_pos 0 0 10})}
#define PSCAN_SWITCH_SCREEN_HANDLERS
((allow_back FALSE)
   (test_time 0)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))
   (TRANSITION_COMPLETE_MSG
      {options set_pscan TRUE}
      {$this set test_time {'+' {taskmgr ui_seconds} 15}})
   (reset_pscan_time
      {> {taskmgr ui_seconds} [test_time]})
   (poll
      {if {&& {$this reset_pscan_time} {options get_pscan}}
         {options set_pscan FALSE}
         {ui goto_screen [done_screen]}})
   (SELECT_START_MSG
      {switch
         $component
         (pscan_yes.btn
            {ui goto_screen [done_screen]})
         (pscan_no.btn
            {options set_pscan FALSE}
            {ui goto_screen [done_screen]})}))
{new GHScreen pscan_switch_screen
   (panels meta pscan_switching_panel helpbar)
   (focus pscan_switching_panel)
   PSCAN_SWITCH_SCREEN_HANDLERS
   (done_screen video_settings_screen)}
{new GHPanel pscan_switching_game_panel
   (file pscan_switching_game.milo)
   (focus pscan_yes.btn)}
{new GHScreen pause_pscan_switch_screen
   (panels GAME_PANELS pscan_switching_game_panel helpbar)
   (focus pscan_switching_game_panel)
   PSCAN_SWITCH_SCREEN_HANDLERS
   (animate_transition FALSE)
   (done_screen pause_video_settings_screen)}
{new SliderPanel pause_video_settings_panel
   (file pause_video_settings.milo)
   VIDEO_SETTINGS_PANEL_HANDLERS
   (pscan_warning pause_pscan_warning_screen)}
{new GHScreen pause_video_settings_screen
   (panels GAME_PANELS pause_video_settings_panel helpbar)
   (focus pause_video_settings_panel)
   (in_game TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (old_lefty_0 0)
   (old_lefty_1 0)
   (stash_lefty_settings TRUE)
   (enter
      {if {! $encorefocus}
         {world_panel set_showing {! $focusmode}}}
      {if_else [stash_lefty_settings]
         {do
            {set [old_lefty_0] {options get_lefty 0}}
            {set [old_lefty_1] {options get_lefty 1}}}
         {set [stash_lefty_settings] TRUE}}
      {if_else {> {game num_players} 1}
         {$this enable {pause_video_settings_panel find gs_left_p2.btn}}
         {$this disable {pause_video_settings_panel find gs_left_p2.btn}}})
   (lefty_changed
      {'||'
         {!= [old_lefty_0] {options get_lefty 0}}
         {!= [old_lefty_1] {options get_lefty 1}}})
   (BUTTON_DOWN_MSG
      {if {== $button kPad_Tri}
         {if_else {$this lefty_changed}
            {do
               {if_else {&& {!= [old_lefty_0] {options get_lefty 0}} {!= [old_lefty_1] {options get_lefty 1}}}
                  {lefty_restart_screen set lefty_switch 2}
                  {if_else {!= [old_lefty_0] {options get_lefty 0}}
                     {lefty_restart_screen set lefty_switch 0}
                     {lefty_restart_screen set lefty_switch 1}}}
               {ui goto_screen lefty_restart_screen}}
            {ui goto_screen pause_screen}}}
      kDataUnhandled)}