#include track_panel.dta
#include hud_panel.dta

{new FadePanel fade_panel
   (file fade.milo)
   (enter)}
{new GHScreen finish_fade
   (panels fade_panel)
   (enter
      {meta_loading_goto qp_selsong_screen})}

{new GHPanel mtv_overlay_panel
   (file mtv_overlay.milo)
   (enter
      {if {== {gamecfg get mode} jukebox}
          {mtv_campaign_song_id.view set_local_pos -380 200 -175}}
      {if_else {== {{ui current_screen} name} credits_game_screen}
         {$this show_overlay FALSE}
         {$this setup_text}})
   (setup_text
      {do
         ($song {gamecfg get_song})
         ($song_text {game get_song_text})
         ($artist_text {game get_song_artist_text})
         ($song_caption {game get_song_caption})
         {do
            ($prefix "mtv_campaign_line")
            {mtv_campaign_song_id.view set_showing TRUE}
            {if_else {!= $speedmod 1}
               {if_else {&& {== {gamecfg get mode} practice} {!= {gamecfg get practice_speed} 0}}
                  {$this set_line $prefix 1 $song_text}
                  {$this set_line $prefix 1 {sprint $song_text " (" {int {'+' 0.5 {'*' $speedmod 100}}} "%)"}}}
               {$this set_line $prefix 1 $song_text}}
            {$this set_line $prefix 2 $song_caption}
            {if_else $author_display
               {if_else {!= "" {localize {sprint {gamecfg get_song} "_author"}}}
                  {$this set_line $prefix 3 {sprint $artist_text "\nAuthor: " {localize {sprint $song "_author"}}}}
                  {$this set_line $prefix 3 {sprint $artist_text "\nAuthor: Unknown"}}}
               {$this set_line $prefix 3 $artist_text}}}})
   (set_line ($prefix $i $text)
      {{sprint $prefix $i ".lbl"} set_localized_text $text}
      {{sprint $prefix $i "_shadow.lbl"} set_localized_text $text})
   (show_overlay ($show)
      {$this set_showing $show}
      {$this set_paused {! $show}})}
{new MidiLoaderPanel midi_loader_panel}
#define LOSE_MUSIC_SLOW_SEC (2)
#define PRACTICE_END_FADE_SEC (1)
{new GamePanel game
   (file "")
   (track_extend_sec -2)
   (load
      {set_loader_period 13})
   (enter
      {rnd show_overlay cheats FALSE}
      {set $interval_p0 50}
      {set $interval_p1 50}
      {set $interval_p2 50}
      {set $player0_streak 0}
      {set $player1_streak 0}
      {set $player2_streak 0}
      {set_loader_period 5}
      {set $clap FALSE}
      {set $oktoclap FALSE}
      {world_panel set_showing TRUE})
   (intro_start_msg ($fast $encore)
      {track_panel intro_start_msg}
      {mtv_overlay_panel show_overlay FALSE}
      {streak_overlay_panel show_overlay FALSE}
      {if $do_streak
         {streak_overlay_panel setup_streak}}
      {script_task
         (delay 1)
         (units kTaskSeconds)
         (script {mtv_overlay_panel show_overlay TRUE})}
      {if {! {== {gamecfg get mode} jukebox}}
         {if_else {! $fast}
               {script_task
                  (delay 6)
                  (units kTaskSeconds)
                  (script
                     {if_else {== {gamecfg get mode} {|| multi_fo multi_vs}}
                        {mtv_overlay_panel show_overlay FALSE}
                        {mtv_overlay_panel show_overlay $songtitle}})}
               {script_task
                  (delay 3)
                  (units kTaskSeconds)
                  (script
                     {if_else {== {gamecfg get mode} {|| multi_fo multi_vs}}
                        {mtv_overlay_panel show_overlay FALSE}
                        {mtv_overlay_panel show_overlay $songtitle}})}}}
      {set [intro_complete] FALSE}
      kDataUnhandled)
   (intro_skip
      {track_panel intro_skip}
      {if $do_streak
         {streak_overlay_panel setup_streak}}
      kDataUnhandled)
   (extend_track
      {if {!= {gamecfg get mode} jukebox}
         {if {|| $focusmode {== {gamecfg get mode} credits}}
            {world_panel set_showing FALSE}
            {if {exists world}
               {world do_force_shot lighter}
               {world toggle_pick_shot}}}}
      {if {&& {! $neckless} {! $cheat_enabled_performance_mode}}
         {track_panel do_extend_sequence}}
      {beatmatch set_music_speed $speedmod}
      {script_task
         (delay 1.8)
         (units kTaskSeconds)
         (script {handle ($this slide_meter_in)})}
      {script_task
         (delay 2)
         (units kTaskSeconds)
         (script
            {handle
               ($this intro_end)
               (streak_overlay_panel show_overlay $do_streak)})}
      {set [intro_complete] TRUE})
   (music_start
      {ui music_start}
      kDataUnhandled)
   (beat
      {if $barf_cheat
         {track_panel animate
            (range 2000 2025)
            (period 0.4)}}
      {if $debug.beep
         {play_sfx click.cue}}
      {if_else {&& {crowd_audio enabled} $oktoclap {>= {world get excitement_level} kExcitementGreat}}
         {set $clap TRUE}
         {set $clap FALSE}}
      {if $clap
         {script_task
            (units kTaskBeats)
            (delay CLAP_DELAY_BEATS)
            (script {handle (world play_sfx CLAP_SEQUENCE)})}}
      kDataUnhandled)
   (downbeat
      {$this foreach_player $p {$p on_downbeat}}
      kDataUnhandled)
   (game_over
      {$this foreach_player $p {$p on_game_over}}
      {stop_sfx sp_deployed}
      {stop_sfx sp_deployed_multi}
      {stop_sfx sp_deploystart.cue}
      {streak_overlay_panel show_overlay FALSE}
      kDataUnhandled)
   (game_won_msg ($encore)
      {if_else {gamecfg get game_over_sequence}
         {do
            {world_panel set_showing TRUE}
            {if {&& $focusmode {exists world}}
               {world toggle_pick_shot}}
            {if {!= {gamecfg get mode} jukebox}
               {do
                  {track_panel explode}
                  {play_game_sfx TRACK_EXPLODE_SFX}}}
            {game set_realtime TRUE}
            {beatmatch set_paused TRUE}
            kDataUnhandled}
         0})
   (game_lost
      {if {&& $focusmode {exists world}}
            {world toggle_pick_shot}}
      {world_panel set_showing TRUE}
      {track_panel set_showing FALSE}
      {game set_realtime TRUE}
      {$this slow_music}
      kDataUnhandled)
   (game_outro_complete
      {synth stop_all_sfx}
      {game set_paused TRUE}
      {if_else [won]
         {ui goto_screen post_show_screen}
         {ui goto_screen {gamecfg get lose_screen}}}
      kDataUnhandled)
   (blew_big_note ($player)
      {if {crowd_audio enabled}
         {world play_sfx blew_bignote.cue}})
   (practice_end
      {music_fader fade -50 {'*' PRACTICE_END_FADE_SEC 1000}}
      {script_task
         (delay PRACTICE_END_FADE_SEC)
         (units kTaskSeconds)
         (script
            {$this set_paused TRUE}
            {if_else $focusmode
               {ui goto_screen practice_end_focus_screen}
               {ui goto_screen practice_end_screen}})})
   (slow_music
      {set [lose_time] {taskmgr seconds}}
      {script_task (once FALSE)
         (script
            {do
               ($t {- {taskmgr seconds} [lose_time]})
               {if_else {< $t LOSE_MUSIC_SLOW_SEC}
                  {do
                     {beatmatch set_music_speed {/ {- LOSE_MUSIC_SLOW_SEC $t} LOSE_MUSIC_SLOW_SEC}}
                     TRUE}
                  {do
                     {beatmatch set_paused TRUE}
                     FALSE}}})})
   (end_streak ($player $playerobj)
      {if {gamecfg get blown_streak_feedback}
         {do ($mult)
            {if_else {== multi_coop {gamecfg get mode}}
               {set $mult {band base_multiplier}}
               {set $mult {$playerobj base_multiplier}}}
            {if {> $mult 1}
               {handle (game blow_streak $player $mult)}}}})
   (blow_streak ($player $mult)
      {do
         ($sfx {sprintf "streak_broken_%ix.cue" $mult})
         {play_game_sfx_player $sfx $sfx $player}}
      kDataUnhandled)
   (lose_time 0)
   (intro_complete FALSE)}
{new UIPanel world_panel
   (file
      {sprint "../world/" {game get_venue} "/" {game get_venue}
         {if_else {game multiplayer}
            {if_else {game multiplayer_vs}
               "_mp.milo"
               "_coop.milo"}
            ".milo"}})
   (finish_load
      {{$this loaded_dir} set_name world main})
   (enter
      {if $focusmode {crowd_audio set_enabled FALSE}}
      {if_else {game multiplayer}
         {handle ($this multi_player)}
         {handle ($this single_player)}}
      {if {&& {|| $focusmode {== {gamecfg get mode} credits}} {!= {gamecfg get mode} jukebox}}
	     {$this set_showing FALSE}}
      {$this set_paused FALSE})}

#define TRACK_MASK ()
#define GAME_PANELS (midi_loader_panel game TRACK_MASK world_panel track_panel hud mtv_overlay_panel streak_overlay_panel)
{new GHScreen game_screen
   (panels GAME_PANELS fade_panel)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (attract_mode FALSE)
   (BUTTON_DOWN_MSG
      {if {&& {== $button kPad_Start} {! {game get game_over}}}
         {game set_paused TRUE}
         {ui goto_screen pause_screen}})
   (poll
      {if {&& {! {ui in_transition}} {game is_missing_controller}}
         {game set_paused TRUE}
         {if_else {game multiplayer}
            {ui goto_screen multi_pause_controller_screen}
            {ui goto_screen pause_controller_screen}}})
   (TRANSITION_COMPLETE_MSG
      {if $game_unpause_on_enter
         {game set_paused FALSE}
         {world_panel set_paused FALSE}
         {set $game_unpause_on_enter FALSE}})
   (enter)}
{new CreditsPanel credits_panel
   (file credits.milo)}
{new GHScreen credits_game_screen
   (panels midi_loader_panel game TRACK_MASK world_panel credits_panel track_panel hud streak_overlay_panel)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (attract_mode FALSE)
   (BUTTON_DOWN_MSG
      {if {== $button kPad_Start}
         {meta_loading_goto {gamecfg get continue_screen}}})
   (poll
      {if {&& {! {ui in_transition}} {game is_missing_controller}}
         {game set_paused TRUE}
         {if_else {game multiplayer}
            {ui goto_screen multi_pause_controller_screen}
            {ui goto_screen pause_controller_screen}}})
   (TRANSITION_COMPLETE_MSG
      {if $game_unpause_on_enter
         {game set_paused FALSE}
         {world_panel set_paused FALSE}
         {set $game_unpause_on_enter FALSE}})
   (enter)}